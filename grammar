program        → declaration* EOF;

declaration    → classDecl
               | funDecl
               | varDecl
               | statement;

classDecl      → "class" IDENTIFIER ( "<" IDENTIFIER )?
                 "{" IDENTIFIER function* "}" ;

funDecl        → "fun" IDENTIFIER function ;

varDecl        → "var" IDENTIFIER ( "=" expression )? ";" ;

statement      → printStmt
               | forStmt
               | ifStmt
               | whileStmt
               | breakStmt
               | returnStmt
               | block
               | exprStmt ;

printStmt      → "print" expression ";" ;

forStmt        → "for" "(" ( varDecl | exprStmt | ";" )
                 expression? ";"
                 expression? ")" statement ;

whileStmt      → "while" "(" expression ")" statement ;

breakStmt      → "break" ";" ;

ifStmt         → "if" "(" expression ")" statement
               ( "else" statement )? ;

function       → "(" parameters? ")" block ;
parameters     → IDENTIFIER ( "," IDENTIFIER )* ;
returnStmt     → "return" expression? ";" ;

block          → "{" declaration* "}" ;

exprStmt       → expression ";" ;


expression     → comma ;
comma          → assignment ( "," assignment )* ;
assignment     → ( call "." )? IDENTIFER "=" assignment
               | logic_or ;
logic_or       → logic_and ( "or" logic_and )* ;
logic_and      → conditional ( "and" conditional )* ;
conditional    → equality ( "?" conditional ":" conditional )? ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;
unary          → ( "!" | "-" ) unary
               | call ;
call           → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary        → NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")"
               | IDENTIFIER
               // error production
               | ( "!=" | "==" ) equality
               | ( ">" | ">=" | "<" | "<=" ) comparison
               | ( "-" | "+" ) term
               | ( "/" | "*" ) factor ;
arguments      → expression ( "," expression )* ;

NUMBER         → DIGIT+ ( "." DIGIT+ )? ;
STRING         → "\"" <any char except "\"">* "\"" ;
IDENTIFIER     → ALPHA ( ALPHA | DIGIT )* ;
ALPHA          → "a" ... "z" | "A" ... "Z" | "_" ;
DIGIT          → "0" ... "9" ;
